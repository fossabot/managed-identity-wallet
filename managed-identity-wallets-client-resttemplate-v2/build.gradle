
plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id "org.openapi.generator" version "6.6.0"
}

group = "${groupName}"
version = "${applicationVersion}"
sourceCompatibility = JavaVersion.VERSION_17

bootJar {
    enabled = false
}

jar {
    enabled = true
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/generated/openapi/src/main/java'
        }
    }
}

ext {

}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation "org.springdoc:springdoc-openapi-starter-common:${openApiVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openApiVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    compileOnly 'javax.servlet:servlet-api:2.5'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    //Added explicitly to mitigate CVE 2022-1471
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'
}

openApiGenerate {
    generatorName.set("java")
    /* for more libraries see https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/java.md */
    library.set("resttemplate")

    inputSpec.set("$rootDir/docs/openapi/v2/openapi.yaml")
    outputDir.set("$buildDir/generated/openapi")

    invokerPackage.set("org.eclipse.tractusx.managedidentitywallets.spring.resttemplate.v2.client")
    apiPackage.set("org.eclipse.tractusx.managedidentitywallets.spring.resttemplate.v2.controllers")
    modelPackage.set("org.eclipse.tractusx.managedidentitywallets.spring.resttemplate.v2.models")
    groupId.set("org.eclipse.tractusx.managedidentitywallets.spring.resttemplate")
    apiNameSuffix.set("V2")
    modelNameSuffix.set("V2")
    generateApiTests.set(false)
    generateModelTests.set(false)
    configOptions.put("delegatePattern", "true")
}
